Start = Rule*

Rule =
  | Comment_eol? [name]ident "=" [choices]Choice Comment_eol
  | Comment_eol? [name]ident "=" Comment_eol ("|" [choices]Choice Comment_eol)+

Choice = [children]Item+ ("--" [name]ident)? (";" [comment]rest_of_line)?

Item = [?neg]"~"? [?lexify]"#"? ("[" [flag]flag? [name]ident "]")? [inner]ItemInner [suffix]suffix?

ItemInner =
  | string
  | ident
  | "(" [:nested]Item+ ")" -- nested
  | char_range
  | char

char_range = "'" [start]single ".." [end]single "'"
char = "'" [char]single "'"

single =
  | "\\" any
  | ~"'" ~'\n' any

string = '"' [@contents]strchar* '"'
strchar =
  | "\\" any
  | ~'"' ~'\n' any

flag =
  | "?" -- bool ; exists
  | ":" -- array
  | "@" -- string ; contents

suffix =
  | "+" -- plus
  | "*" -- star
  | "?" -- opt

ident = ~"0" identchar+
identchar =
  | 'a..z'
  | 'A..Z'
  | '0..9'
  | '_'

rest_of_line = (~"\n" any)*

Comment_eol = ((";" rest_of_line)? eee)+

eol = white* eee
eee =
  | eolchar+
  | EOF
eolchar =
  | "\n"
  | "\r"
white =
  | " "
  | "\t"
