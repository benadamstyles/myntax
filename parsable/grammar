@lineComment(";")
@blockComment("/*", "*/")
Start = Rule*

Decorator = "@" [name]ident ("(" ([args]decarg ",")* [args]decarg? ")")? __comment_eol

decarg =
  | bool -- bool
  | string -- string
  | number -- number

@leaf
bool =
  | "true"
  | "false"

Rule =
  | [decorators]Decorator* [name]ident "=" [choices]Choice __comment_eol
  | [decorators]Decorator* [name]ident "=" __comment_eol ("|" [choices]Choice __comment_eol)+

Choice = [children]Item+ (
  "--" [name]ident
)? (
  ";" [comment]rest_of_line
)?

Item = [?neg]"~"? [?lexify]"#"? ("[" [flag]flag? [name]ident "]")? [inner]ItemInner [suffix]suffix?

ItemInner =
  | string
  | ident
  | "(" NestedItems ")" -- nested
  | char_range
  | char

@passThrough
@ignoreNewlines(true)
NestedItems = [:nested]Item+

char_range = "'" [start]single ".." [end]single "'"
char = "'" [char]single "'"

@leaf
single =
  | "\\" any
  | ~"'" ~'\n' any

@leaf
string = '"' [@contents]strchar* '"'
strchar =
  | "\\" any
  | ~'"' ~'\n' any

flag =
  | "?" -- bool ; exists
  | ":" -- array
  | "@" -- string ; contents

suffix =
  | "+" -- plus
  | "*" -- star
  | "?" -- opt

@leaf
ident = ~digit identchar+
identchar =
  | 'a..z'
  | 'A..Z'
  | '0..9'
  | '_'

@leaf
number =
  | "0" ~identchar
  | ~"0" digit+ ~identchar
digit = '0..9'

rest_of_line = (~"\n" any)*

eol = white* eee
eee =
  | eolchar+
  | EOF
eolchar =
  | "\n"
  | "\r"
white =
  | " "
  | "\t"
