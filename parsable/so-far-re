@lineComment("//")
@blockComment("/*", "*/")
Start = ModuleBody

ModuleDesc =
  | "{" ModuleBody "}" -- structure
  | [ident]longCap -- ident
  ; TODO functor
  ; TODO apply
  ; TODO constraint
  ; TODO unpack
  ; TODO extension

@passThrough
@ignoreNewlines(false)
ModuleBody = EOL? (Structure EOL)*

Structure =
  | "let" "module" capIdent "=" ModuleDesc -- let_module
  | "let" "rec" "module" capIdent "=" "{" ModuleBody "}" (EOL "and" capIdent "=" "{" ModuleBody "}")* -- let_rec_module
  | "let" [rec]"rec"? ValueBinding (EOL "and" ValueBinding)* -- value
  | "type" TypeDeclaration (EOL+ "and" TypeDeclaration)* -- type
  | "module" "type" capIdent "=" ModuleTypeDeclaration -- module_type
  | "exception" capIdent "of" Type
  | "import" "*" "from" longCap -- open
  | "include" "*" "from" longCap -- include
  | "[@@@"& lowerIdent &"]" -- attribute
  | "[%%"& lowerIdent &"]" -- extension
  | Expression Attribute* -- eval
  ; class, class type
  ; typeext
  ; external "primitive"

ModuleTypeDeclaration = lowerIdent ; TODO

; TODO class field
; TODO class expr
; TODO class type field
; TODO class type

TypeDeclaration =
  | lowerIdent "=" longIdent "=" TypeDecl -- manifested
  | lowerIdent "=" TypeDecl -- decl
  | lowerIdent -- ident

TypeDecl =
  | TypeVariant
  | TypeRecord
  | Type

@ignoreNewlines(false)
TypeVariant = 
  | "enum" "{" EOL? (TypeVariantItem EOL)* TypeVariantItem? "}"
  | capIdent ("|" capIdent)+ ; should I have this?

TypeVariantItem =
  | "case" capIdent "(" TypesCommad ")"
  | "case" capIdent

@ignoreNewlines
@passThrough
TypesCommad = (Type &",")* Type?

@ignoreNewlines
TypeRecord = "{" (lowerIdent &":" Type ",")* (lowerIdent &":" Type)? "}"

ValueBinding =
  | [name]lowerIdent "("& ValueArgs &")" "=>" Expression -- func
  | [name]lowerIdent Arg "=>" Expression -- func
  | Pattern "=" Expression -- value

@passThrough
@ignoreNewlines
ValueArgs = (Arg &",")* Arg?

Arg =
  | "::"& lowerIdent &ArgValue? -- punned
  | lowerIdent &"::"& Pattern &ArgValue? -- named
  | Pattern -- anon

ArgValue =
  | "=" &"?" -- none
  | "=" &Expression -- expr

Pattern =
  | Pattern "as" lowerIdent
  | Pattern &":" Type
  | "()" -- unit
  | "_" -- ignore
  | Pattern "|" Pattern -- or
  | "(" Pattern &":" Type ")" -- constraint
  | "lazy" Pattern -- lazy
  | "exception" Pattern -- exception
  | "[%" lowerIdent "]" -- extension
  | char &".."& char -- interval
  | "("& Pattern (&"," Pattern)+ &","? &")" -- tuple
  | "[|"& (Pattern &",")* Pattern? &"|]" -- array
  | "["& (Pattern &",")* Pattern? ("..."& [rest]lowerIdent)? &"]" -- list
  | "{"& (longIdent (&":" Pattern)? &",")* (longIdent (&":" Pattern)?)? &"}" -- record ; TODO _ for open flag
  | [isVariant]"`"&? capIdent &("("& (Pattern &",")* Pattern? &")")?  -- constructor
  | lowerIdent -- ident
  | constant -- const
  ; Ppat_type
  ; Ppat_unpack

Type =
  | Type "as" typeVar -- alias
  | "("& TupleTypes &")" -- tuple
  | "_" -- any
  | typeVar -- var
  | (lowerIdent &"?"? &"::"&) Type "=>" Type
  | longIdent (&"<"& TypesCommad &">"&)? -- constructor
  ; TODO object
  ; TODO class
  ; TODO alias?
  ; TODO package
  ; TODO extension

@passThrough
@ignoreNewlines
TupleTypes = Type (&"," Type)+ ","?

typeVar = "'" lowerIdent

Attribute = "[@@" lowerIdent Expr? "]"

@ignoreNewlines(false)
Statement =
  | "let" [rec]"rec"? ValueBinding (EOL "and" ValueBinding)* -- value
  | "let" "module" capIdent "=" ModuleDesc -- module
  | "import" "*" "from" longCap -- import
  | "assert" Expression -- assert
  | "lazy" Expression -- lazy
  | lowerIdent "=" Expression -- instance_var
  | Expression -- expr

Expression =
  | [condition]BinExpression "?" [consequent]BinExpression ":" [alternate]BinExpression -- ternary
  | BinExpression -- binary
  | BaseExpression -- base ; just for printing

;; maybe get operator precedence going?
;; I'll do it in the code portion. *much* less cumbersom that way
BinExpression =
  ; TODO Base ([op] Base)+
  | BaseExpression ([op]binOp BaseExpression)+ -- binexp
  | BaseExpression -- base

BaseExpression =
  | unOp& BaseExpression -- unexp
  | Application -- application
  | FunExpr -- funexpr
  | SwitchExp -- switch
  | IfExpr -- if
  | TryExp -- try
  | "("& binOp &")" -- binop
  | "("& TupleBody &")" -- tuple
  | "[|"& (Expression &",")* Expression? &"|]" -- array
  | Block -- block
  | "{"& RecordBody &"}" -- record
  | "["& Commas &"]" -- list
  | [isVariant]"`"&? longCap (&"("& Commas &")")? -- constructor
  | BaseExpression &"."& longIdent "=" Expression -- set_attr
  | BaseExpression &"."& longIdent -- get_attr
  ; TODO ternary a level above base?
  ; | Expression "?" Expression ":" Expression -- ternary
  | "while" Expression Block -- while
  ; | "for" -- for ; maybe ditch?
  | "new" longIdent -- new
  | BaseExpression &"#"& lowerIdent -- send
  | longIdent -- ident
  | constant -- const
  | "()" -- unit
  | "("& WrappedExpression &")" -- wrapped ; or constraint
  | "[%"& lowerIdent &"]" -- extension
  ; TODO coerce
  ; TODO override
  ; TODO poly
  ; TODO object
  ; TODO newtype
  ; TODO pack

@ignoreNewlines
@passThrough
WrappedExpression = Expression (&':' Type)?

@ignoreNewlines
@passThrough
TupleBody = Expression (&"," Expression)+ &","?

@ignoreNewlines
@passThrough
RecordBody = ("..."& Expression &",")? (RecordItem &",")* RecordItem?
RecordItem = longIdent (&":" Expression)?

IfExpr = "if" Expression Block ("else" "if" Expression Block)* ("else" Block)?

SwitchExp = "switch" Expression "{" SwitchBody "}"
SwitchBody = EOL (SwitchCase EOL)*
SwitchCase = "case" Pattern ("with" [guard]Expression)? &":" [body]Expression?

TryExp = "try" Block "catch" "{" SwitchBody "}"

FunExpr =
  | "("& ValueArgs &")" "=>" Expression -- multi
  | Arg "=>" Expression -- single

Block = "{" BlockBody "}"

@ignoreNewlines(false)
@passThrough
BlockBody = EOL? (Statement EOL)* Statement?

@passThrough
Application = Expression &"("& AppArgs &")"

@passThrough
@ignoreNewlines
AppArgs = (FnArg &",")* FnArg?

@leaf
unOp =
  | "-" ~opChar
  | "-." ~opChar
  | "?" opChar+
  | unOpChar opChar*

@leaf
binOp = 
  | "!="
  | "or"
  | "mod"
  | ":" opChar+
  | ~unOpChar ~":" ~"?" ~"." ~"//" ~"/*" opChar+

unOpChar =
  | "!"
  | "~"
  ; | "?"

opChar =
  |"!"
  |"$"
  |"%"
  |"&"
  |"*"
  |"+"
  |"-"
  |"."
  |"/"
  |":"
  |"<"
  |"="
  |">"
  |"?"
  |"@"
  |"^"
  |"|"
  |"~"

longIdent = (longCap_ ".")? lowerIdent
longCap = longCap_ ~"."

longCap_ =
  | longCap_ "." capIdent -- dot
  | capIdent -- lident

@passThrough
@ignoreNewlines
Commas = (Expression &",")* Expression?

FnArg =
  | "::"& lowerIdent -- punned
  | lowerIdent &"::"& Expression -- named
  | Expression -- anon

constant =
  | [val]int64 -- int
  | [val]string -- string
  | [val]char -- char

@leaf
capIdent = ~reserved 'A..Z' identchar*
@leaf
lowerIdent = ~reserved 'a..z' identchar*

identchar =
  | alpha
  | digit
  | "_"

@leaf
int64 =  digit+ ~identchar

@leaf
string = "\"" strchar* "\""
strchar =
  | "\\" any
  | ~"\"" ~"\n" ~"\\" any

@leaf
char = "'" charchar "'"
charchar =
  | "\\" any
  | ~"'" ~"\n" ~"\\" any

reserved =
  | "fun"
  | "let"
  | "and"
  | "as"
  | "type"
  | "exception"
  | "of"
  | "module"
  | "rec"
  | "open"
  | "import"
  | "try"
  | "catch"
  | "from"

alpha =
  | 'a..z'
  | 'A..Z'
digit = '0..9'
